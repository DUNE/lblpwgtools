%!GLoBES


/*v2 change efficiencies to be channel based*/

/* Baseline */
$profiletype =          1 
$baseline =             1300

/* Beam data */
nuflux(#fnal_plus)<
  @flux_file="../../fluxes/dusel120e250i002dr280dz_tgtz30_1300km_0kmoa_flux.txt"
  @time = NUTIME /* years */
  @power = (BEAMPOWER*(1.98e7)/(BEAMENERGY*1.6022e-10))/10e16  /* 10^20 POT per year (708kW,1.98e7s per yr,120GeV) */
  @norm = 1.0544e17 
>

/* Detector definition, channels, rules */

/* Fiducial target mass [kt] */
$target_mass = 	     LAMASS

$version="3.0.0"
/* Cross sections */
include "cross_sections.inc"

/* ----------------------------------------------------------------- */
/*             Detector properties : Liquid Argon                    */
/* ----------------------------------------------------------------- */

/* Binning */
/* $emin =                 0.5 */
/*$emax =                20.0 */
/*$bins =               150 */

/*$sampling_min =        0.5 */
/*$sampling_max =       20.0 */

$emin =                 0.5
$emax =                 60.0
$binsize= {0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125,0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125,0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125,0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 2, 2, 2, 2, 2, 2, 2, 2, 2, 
2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 4}


$sampling_min =                 0.5
$sampling_max =                 60.0
$sampling_stepsize= {0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125, 0.125,0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125, 0.125,0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125, 0.125,0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 
0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 0.125, 2, 2, 2, 2, 2, 2, 2, 
2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2, 2}

/* Technical information */
$filter_state = 0
$filter_value = 0.125


/* Energy Resolution */

energy(#mucc)<
        @type = 1
        @sigma_e = {0.0,0.15,0.0}
>

energy(#muqe)<
        @type = 1
        @sigma_e = {0.0,0.05,0.0}
>

energy(#elec)<
        @type = 1
        @sigma_e = {0.0,0.15,0.0}
>

energy(#hadr)<
        @type = 1
        @sigma_e = {0.0,0.40,0.0}
>

energy(#nc)<>
include "../../smear/nc_smear_nu_ereco.dat"


%sbc = samplingbincenter()
%bc = bincenter()

/* ----------------------------------------------------------------- */
/*                                Channels                           */
/* ----------------------------------------------------------------- */


channel(#nu_mu_cc_app)<
        @channel =      #fnal_plus:     +:      m:      m:      #CC:    #mucc
        @post_smearing_efficiencies = interpolation({0,100},{0.02,0.02},1,%bc)
>

channel(#nu_mu_cc_dis)<
        @channel =      #fnal_plus:     +:      m:      m:      #CC:    #mucc
        @post_smearing_efficiencies = interpolation({0,100},{0.85,0.85},1,%bc)
>

channel(#nu_mu_qe)<
        @channel =      #fnal_plus:     +:      m:      m:      #QE:    #muqe
>

/*#nu_mu_disappearance_NC*/
channel(#nu_mu_nc)<
        @channel =      #fnal_plus:     +:      m:  NOSC_m:      #NC:    #nc
        @post_smearing_efficiencies = interpolation({0,100},{0.02,0.02},1,%bc)

>
channel(#nu_mu_nc_dis)<
        @channel =      #fnal_plus:     +:      m:  NOSC_m:      #NC:    #nc
        @post_smearing_efficiencies = interpolation({0,100},{0.01,0.01},1,%bc)

>
/*#nu_e_beam*/
channel(#nu_e_beam)<
        @channel =      #fnal_plus:     +:      e:      e:      #CC:    #elec
        @post_smearing_efficiencies = interpolation({0,100},{0.95,0.95},1,%bc)
>
/*#nu_e_bar_beam*/
channel(#nu_e_bar_beam)<
        @channel =      #fnal_plus:     -:      e:      e:      #CC:    #elec
        @post_smearing_efficiencies = interpolation({0,100},{0.95,0.95},1,%bc)
>

/*#nu_e_appearance_CC*/
channel(#nu_e_sig)<
        @channel =      #fnal_plus:     +:      m:      e:      #CC:    #elec
        @post_smearing_efficiencies = interpolation({0,100},{0.95,0.95},1,%bc)
>


/* ----------------------------------------------------------------- */
/*                    Rules for a Liquid Argon Detector              */
/* ----------------------------------------------------------------- */

/* CC */
rule(#nu_e_appearance)<
  @signal = 1.0@#nu_e_sig
  @background =  1.0@#nu_mu_cc_app  : 1.0@#nu_e_beam : 1.0@#nu_mu_nc 
  @backgrounderror = 0.05 : 0.0001
  @signalerror = 0.01 : 0.0001
  @sys_on_function = "chiSpectrumTilt"
  @sys_off_function = "chiNoSysSpectrum"
>

rule(#nu_mu_disappearance)<
  @signal = 1.0@#nu_mu_cc_dis				
  @signalerror = 0.05 : 0.02				
  @background = 1.0@#nu_mu_nc_dis
  @backgrounderror = 0.1 : 0.02			
  @sys_on_function = "chiSpectrumTilt"
  @sys_off_function = "chiNoSysSpectrum"
>



